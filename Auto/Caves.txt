var if = import Personal/Factory/Conditionals

func grind()
  
  ?if.enemyIsBoss()
    equip heavy crossbow

  :?foe.count > 5 & item.GetCooldown("blade") <= 0
    equip blade
    
    ?item.CanActivate("blade")
      activate R

  :?if.pickupNearby()
    equipL triskelion
    equipR star

  :?item.GetCooldown("quarterstaff") <= 0
    equip quarterstaff

    ?item.CanActivate("quarterstaff")
      activate R

  :?if.enemyIsInDashRange()
    equipL poison wand
    equipR dashing shield

  :?if.enemyIsNearby(10)
    equipL ice wand *7
    equipR ice wand *6

  :?if.pickupNearby()
    equipL triskelion
    equipR star

  :?if.characterIsFullHealth()
    equipL triskelion
    equipR ice shield

  :
    equipL ouroboros
    equipR ice shield

func push()
  
  ?if.enemyIsBoss()
    ?foe.debuffs.GetCount("debuff_damage") = 1 & foe.debuffs.GetCount("debuff_chill") = 6
      equip heavy crossbow
    :
      equipL poison wand dP
      equipR ice wand dI
  
  :?item.GetCooldown("quarterstaff") <= 0
    equip quarterstaff
    
    ?item.CanActivate("quarterstaff")
      activate R

  :?if.enemyIsInDashRange()
    equipL ice wand dI
    equipR dashing shield

  // if enemies are within blade range, abuse them with bfg
  :?if.enemyIsNearby(10)
    equip blade
    
    ?item.CanActivate("blade") & foe.count > 10
      activate R

  :?if.pickupNearby()
    equipL triskelion
    equipR star

  :
    // last ditch attempt to make up for any lost health
    equipL ice wand dI
    equipR ice shield

?loc = Caves
  ?loc.begin | loc.loop
    equipL triskelion
    equipR tower shield

  ?hp <= 10
    activate potion

  ?loc.stars = 5
    grind()
  :
    push()