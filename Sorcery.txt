import UI/MindstoneButton

// Location automation
import Personal/Auto/Caves

// Conditional functions
var if = import Personal/Factory/Conditionals

?loc = Forest
  ?foe = boss
    ?hp = maxhp
      equipL poison wand *5
      equipR tower shield
    :
      ?foe.distance > 50
        equipL ouroboros
        equipR tower shield
      :
        equipL poison wand *5
        equipR poison wand *4
  :?foe.count = 0
    equipL ouroboros
    equipR tower shield
  :
    ?foe.distance > 10
      equipL poison wand *5
      equipR triskelion
    :
      equipL poison wand *5
      equipR tower shield

?loc = Rocky | loc = Deadwood
  ?if.enemyIsNearby()
    ?harvest.distance > 5
      equipL triskelion
      equipR star
    :
      ?if.characterTargettingTree()
        equipR hatchet
        ?item.CanActivate("hatchet")
          activate R
      :?if.characterTargettingBoulder()
        equip shovel
  :
    ?foe = boss
      equipL poison wand
      equipR tower shield
    :
      ?foe = immune_to_ranged
        equipL sword
        equipR tower shield
      :
        equipL crossbow
        equipR tower shield
        